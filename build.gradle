plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '2.0.0'
}

repositories {
    mavenCentral()
}

dependencies {
    // Find dependencies on https://central.sonatype.com/

    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    // Test Dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.8.2'
}

application {
    mainClass.set('algs4_practices.AppKotlinKt')
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11"
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = "11"
    targetCompatibility = "11"
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        kotlin {
            srcDirs = ['src/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        kotlin {
            srcDirs = ['src/test/java']
        }
    }
}

tasks.named('jar') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': application.mainClass.get()
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

run {
    standardInput = System.in
}

test {
    useJUnitPlatform()
}
