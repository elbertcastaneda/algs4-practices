import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '2.0.21'
}

repositories {
    mavenCentral()
}

dependencies {
    // Find dependencies on https://central.sonatype.com/

    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    // Test Dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.3'
}

application {
    mainClass.set('algs4_practices.AppKt')
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11"
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = "11"
    targetCompatibility = "11"
    // options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        kotlin {
            srcDirs = ['src/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        kotlin {
            srcDirs = ['src/test/java']
        }
    }
}

tasks.named('jar') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': application.mainClass.get()
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

run {
    standardInput = System.in
}

test {
    useJUnitPlatform()
}

tasks.test {
    // Show test results in the console
    testLogging {
        events("passed", "skipped", "failed")
        showStandardStreams = true // Show output from System.out and System.err
        exceptionFormat = 'full'   // Show full exception stack trace
    }
}